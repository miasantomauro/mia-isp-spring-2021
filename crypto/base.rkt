#lang forge

/*
  Base model of strand space (style) crypto
  Tim Mia Abby
  Opting to build in normal Forge, not Electrum
*/

abstract sig mesg {} -- CPSA name for any term

abstract sig Key extends mesg {}
abstract sig akey extends Key {}
sig skey extends Key {}
sig PrivateKey extends akey {}
sig PublicKey extends akey {}

-- Cannot just write fun akey: set Key { PublicKey + PrivateKey }
--   since the macro expands variable declarations to field definitions,
--   and those need to be in terms of sigs

-- relation to match key pairs -- 

one sig KeyPairs {
  pairs: set PrivateKey -> PublicKey,
  owners: set PrivateKey -> name,
  ltks: set name -> name -> skey
}

fun getLTK[name_a: name, name_b: name]: lone skey {
  (KeyPairs.ltks)[name_a][name_b]
}

fun getInv[k: Key]: one Key {
  (k in PublicKey => ((KeyPairs.pairs).k) else (k.(KeyPairs.pairs)))
  +
  (k in skey => k else none)
}


-- t=0, t=1, ...
sig Timeslot {
  next: lone Timeslot
}

-- As names are sent messagest, they learn pieces of data --
sig name extends mesg {
  learned_times: set mesg -> Timeslot,
  generated_times: set text -> Timeslot,
  workspace: set Timeslot -> mesg -> mesg
}

sig strand {
  -- the name associated with this strand
  agent: one name
}
one sig AttackerStrand extends strand {}

one sig Attacker extends name {}

sig Ciphertext extends mesg {
   encryptionKey: one Key,
   -- result in concating plaintexts
   plaintext: set mesg
}

-- Non-name base value (e.g., nonces)
sig text extends mesg {}

-- {foo}_B
-- Note: this is NOT the same as "mesg"
sig Message {
  -- Support delays, non-reception
  sender: one strand,
  receiver: one strand,
  sendTime: one Timeslot,
  data: set mesg
}

pred wellformed {
  -- Design choice: only one message event per timeslot;
  --   assume we have a shared notion of time
  
 all t: Timeslot | lone sendTime.t 

  -- You cannot send a message with no data
  all m: Message | some m.data

  -- someone cannot send a message to themselves
  all m: Message | m.sender not in m.receiver


  -- workspace: workaround to avoid cyclic justification within just deconstructions
  --  e.g., knowing or receiving enc(x, x)
  all d: mesg | all t: Timeslot | all a: name | all superterm: mesg | d in a.workspace[t][superterm] iff {
    -- received in the clear just now (base case)
    {some m: Message | {d in m.data and t = m.sendTime and m.receiver.agent = a} and superterm = d}
    or
    -- breaking down a ciphertext we learned before, or that we've produced from something larger this timeslot
    --     via a key we learned before, or that we've produced from something larger in this timeslot
    {superterm in Ciphertext and
     d in superterm.plaintext and     
     superterm in (a.learned_times).(Timeslot - t.^next) + a.workspace[t][mesg] and
     getInv[superterm.encryptionKey] in (a.learned_times).(Timeslot - t.^next) + a.workspace[t][mesg] 
    }  
  }
 
  -- names only learn information that associated strands are explicitly sent 
  all d: mesg | all t: Timeslot | all a: name | d->t in a.learned_times iff {
    -- they have not already learned the mesg -- 
    {d not in (a.learned_times).(Timeslot - t.*next)} and 

    -- They received a message directly containing d (may be a ciphertext)
    {{some m: Message | {d in m.data and t = m.sendTime and m.receiver.agent = a}}
    or 

    -- deconstruct encrypted term 
    -- constrain time to reception to avoid cyclic justification of knowledge. e.g.,
    --    "I know enc(other-agent's-private-key, pubk(me)) [from below via construct]"
    --    "I know other-agent's-private-key [from above via deconstruct]""
    -- instead: separate the two temporally: deconstruct on recv, construct on non-reception?
    -- in that case, the cycle can't exist in the same timeslot
    -- might think to write an accessibleSubterms function as below, except:
    -- consider: (k1, enc(k2, enc(n1, invk(k2)), invk(k1)))
    -- or, worse: (k1, enc(x, invk(k3)), enc(k2, enc(k3, invk(k2)), invk(k1)))
    { {some m: Message | m.receiver.agent = a and m.sendTime = t}
      d in a.workspace[t][mesg] -- derived from deconstructing anything in this timeslot
    }   
    or 
    -- construct encrypted terms (only allow at NON-reception time; see above)
    -- NOTE WELL: if ever allow an agent to send/receive at same time, need rewrite 
    {d in Ciphertext and 
	   d.encryptionKey in (a.learned_times).(Timeslot - t.^next) and        
	   d.plaintext in (a.learned_times).(Timeslot - t.^next)
     {no m: Message | m.receiver.agent = a and m.sendTime = t} -- non-reception
     }
    or 

    -- name knows all public keys
    {d in PublicKey}
    or
    -- name knows the private keys it owns
    {d in PrivateKey and a = d.(KeyPairs.owners)}
    -- name knows long-term keys they are party to
    or
    {some a2 : name - a | d in getLTK[a, a2] + getLTK[a2, a] }
    or 
    -- names know their own names
    {d = a}
    or
    -- This was a value generated by the name in this timeslot
    {d in (a.generated_times).t}    
    }} 
  
  -- If you generate something, you do it once only
  all a: name | all d: text | lone t: Timeslot | d in (a.generated_times).t

  -- Messages comprise only values known by the sender
  all m: Message | m.data in (((m.sender).agent).learned_times).(Timeslot - (m.sendTime).^next) 

  all m: Message | m.sender = AttackerStrand or m.receiver = AttackerStrand 

  -- plaintext relation is acyclic  
  --  NOTE WELL: if ever add another type of mesg that contains data, + inside ^.
  all d: mesg | d not in d.^(plaintext)

  all c: Ciphertext | some c.plaintext

  (KeyPairs.pairs).PublicKey = PrivateKey -- total
  PrivateKey.(KeyPairs.pairs) = PublicKey -- total
  all privKey: PrivateKey | {one pubKey: PublicKey | privKey->pubKey in KeyPairs.pairs} -- uniqueness
  all priv1: PrivateKey | all priv2: PrivateKey - priv1 | all pub: PublicKey | priv1->pub in KeyPairs.pairs implies priv2->pub not in KeyPairs.pairs

  -- at most one long-term key per (ordered) pair of names
  all a:name, b:name | lone getLTK[a,b]

  -- Attacker's strand
  AttackerStrand.agent = Attacker
}

fun subterm[supers: set mesg]: set mesg {
  -- VITAL: if you add a new subterm relation, needs to be added here, too!
  supers +
  supers.^(plaintext) -- union on new subterm relations inside parens
}
-- Problem: (k1, enc(k2, enc(n1, invk(k2)), invk(k1)))
--  Problem: (k1, enc(x, invk(k3)), enc(k2, enc(k3, invk(k2)), invk(k1)))
--    needs knowledge to grow on the way through the tree, possibly sideways
-- so this approach won't work
/*fun accessibleSubterms[supers: set mesg, known: set mesg]: set mesg {
  -- VITAL: ditto above 
  -- plaintext: Ciphertext -> set mesg
  -- set of Ciphertexts this agent can now open: 
  let openable = {c: Ciphertext | getInv[c.encryptionKey] in known} |
    supers + 
    supers.^(plaintext & (openable -> mesg))
}*/

-- Note it's vital this definition is about strands, not names
pred originates[s: strand, d: mesg] {

  -- unsigned term t originates on n in N iff
  --   term(n) is positive and
  --   t subterm of term(n) and
  --   whenever n' precedes n on the same strand, t is not subterm of n'

  some m: sender.s | { -- messages sent by strand s (positive term)     
      d in subterm[m.data] -- d is a sub-term of m     
      all m2: (sender.s + receiver.s) - m | { -- everything else on the strand
          -- ASSUME: messages are sent/received in same timeslot
          {m2.sendTime in m.sendTime.^(~(next))}
          implies          
          {d not in subterm[m2.data]}
      }
  }
}

pred generates[s: strand, d: mesg] {
  some ((s.agent).generated_times)[d]
}

pred temporary {
  -- upper bounds for one sig have size > 1 at the moment; fix
  one Attacker

  -- for checking, debugging:
  all a1, a2: name | { 
    -- If one has a key, keys are different
    (some KeyPairs.owners.a1 and a1 != a2) implies 
      (KeyPairs.owners.a1 != KeyPairs.owners.a2)
  }

  -- TODO move some of these into the wellformedness pred
  
  all p: PrivateKey | one p.(KeyPairs.owners) 

  agent.Attacker = AttackerStrand

  -- number of keys greater than 0
  #Key > 0
}

/*


-- 2 publickey, 3 ciphertext, 3 agent
-- Sigs that we have: Datum, Key, PrivateKey, PublicKey, SymmetricKey, Agent, Attacker, Ciphertext, Text, Message, Timeslot, KeyPairs

option verbose 5
option solver MiniSatProver
option logtranslation 1
option coregranularity 1
option core_minimization hybrid

run {
  temporary
  wellformed
  ns_execution
  constrain_skeletonNS_0
  constrain_skeletonNS_1
  exploit_search
} for exactly 16 mesg, exactly 6 Key, 
			exactly 2 SkeletonNS, 
			exactly 1 SkeletonNS_0,
 		 	exactly 1 SkeletonNS_1, 
			exactly 3 PrivateKey, 
			exactly 3 PublicKey, 
			exactly 0 skey, 
			exactly 1 Init, 
			exactly 1 Resp,
                        exactly 3 strand,
			exactly 1 Attacker,
                        exactly 1 AttackerStrand,
			exactly 5 Ciphertext, 
			exactly 2 text,
			exactly 6 Message,
 			exactly 6 Timeslot, 
			exactly 1 KeyPairs, 
			exactly 3 name for {next is linear}

*/